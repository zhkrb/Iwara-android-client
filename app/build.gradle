apply plugin: 'com.android.application'

static def gitVersionCode() {
    def cmd = 'git rev-list HEAD --first-parent --count'
    cmd.execute().text.trim().toInteger()
}

static def gitVersionTag(boolean isPattern) {
    def cmd = 'git describe --tags'
    def version = cmd.execute().text.trim()

    if (isPattern){
        def pattern = "-(\\d+)-g"
        def matcher = version =~ pattern

        if (matcher) {
            version = version.substring(0, matcher.start()) + "." + matcher[0][1]
        } else {
            version = version + ".0"
        }
    }

    return version
}

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.zhkrb.iwara"
        minSdkVersion 22
        targetSdkVersion 28
        versionCode 2
        versionName "0.0.3"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
    }
    buildTypes {
        debug {
            // 为了不和 release 版本冲突
            applicationIdSuffix ".debug"
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility = '1.8'
        targetCompatibility = '1.8'
    }

    applicationVariants.all{ variant ->
        if (variant.buildType.name == 'release') {
            variant.outputs.each { output ->
                output.versionNameOverride = gitVersionTag(true)
                output.versionCodeOverride = gitVersionCode()
            }
        }else if (variant.buildType.name == 'debug') {
            variant.outputs.each { output ->
                output.versionNameOverride = gitVersionTag(false)+'-debug'
                output.versionCodeOverride = gitVersionCode()
            }
        }
    }


}

repositories {
    flatDir {
        dirs 'libs', '../libs'
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation('com.android.support:appcompat-v7:23.0.1')
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    implementation 'com.android.support:multidex:1.0.3'
    implementation 'com.google.android.material:material:1.1.0-alpha10'
    implementation 'com.android.support:support-annotations:28.0.0'
    implementation 'com.android.support:swiperefreshlayout:28.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.0-beta2'

    //glide
    implementation 'com.github.bumptech.glide:glide:4.8.0'

    implementation 'com.alibaba:fastjson:1.2.58'

    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    implementation 'io.reactivex.rxjava2:rxjava:2.2.6'

    implementation 'com.squareup.retrofit2:retrofit:2.5.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.3.0'



    implementation 'org.jsoup:jsoup:1.11.3'
    api project(':dragvideo')

}
